apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

tasks.register('sourceJar', Jar) {
  description = 'Packages up the source in a Jar for Maven publishing'
  from sourceSets.main.allJava
  from sourceSets.main.resources
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

tasks.register('javadocJar', Jar) {
  dependsOn javadoc
  description = 'Packages up the javadoc in a Jar for Maven publishing'
  from javadoc.destinationDir
}

artifacts {
  archives sourceJar, javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourceJar {
        archiveClassifier = 'sources'
      }

      artifact javadocJar {
        archiveClassifier = 'javadoc'
      }

      pom.withXml {
        asNode().children().last() + {
          resolveStrategy = Closure.DELEGATE_FIRST
          name        "$title"
          description "$description"
          url         "$website"

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url  'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          organization {
            name 'Allette Systems'
            url  'https://www.allette.com.au'
          }

          scm {
            url                 "git@github.com:pageseeder/${gitName}.git"
            connection          "scm:git:git@github.com:pageseeder/${gitName}.git"
            developerConnection "scm:git:git@github.com:pageseeder/${gitName}.git"
          }

          developers {
            developer {
              id 'clauret'
              name 'Christophe Lauret'
              email 'clauret@weborganic.com'
            }
            developer {
              id 'asantos'
              name 'Alberto Santos'
              email 'asantos@allette.com.au'
            }
            developer {
              id 'jbreure'
              name 'Jean-Baptiste'
              email 'jbreure@weborganic.com'
            }
          }

        }
      }

    }
  }

  // The repository to publish to, Sonatype/MavenCentral
  repositories {
    maven {
      def releaseRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
      url = version.endsWith("SNAPSHOT") ? snapshotRepoUrl : releaseRepoUrl
      name = "sonatype"
      credentials {
        username = findProperty('sonatypeUsername')
        password = findProperty('sonatypePassword')
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

nexusStaging {
  packageGroup = group
  stagingProfileId = findProperty('sonatypeStagingProfileId')
  serverUrl = "https://s01.oss.sonatype.org/service/local/"
  username = findProperty('sonatypeUsername')
  password = findProperty('sonatypePassword')
}